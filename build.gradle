plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

defaultTasks("clean", "compileJava", "compileTestJava")

ext {
	karateVersion = '0.9.6'
	lombokVersion = '1.18.12'
}

group = 'com.iv'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//lombok
	compileOnly "org.projectlombok:lombok:$lombokVersion"
	annotationProcessor "org.projectlombok:lombok:$lombokVersion"

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation "org.projectlombok:lombok:$lombokVersion"
	testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	testImplementation "com.squareup.okhttp3:mockwebserver:3.14.9"

	testImplementation 'org.springframework:spring-test'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'

	testImplementation "com.intuit.karate:karate-junit5:$karateVersion"
	testImplementation "com.intuit.karate:karate-apache:$karateVersion"
}

sourceSets {
	test {
		resources {
			srcDir file('src/test/java')
			exclude '**.*.java'
		}
	}
}

test {
	useJUnitPlatform()
	systemProperty "karate.options", System.properties.getProperty("karate.options")
	systemProperty "karate.env", System.properties.getProperty("karate.env")
	outputs.upToDateWhen { false }
	testLogging.showStandardStreams = true
}

